apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "com.google.gms.google-services"
apply plugin: "maven-publish"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.dagger.hilt.android"
apply plugin: "app.cash.sqldelight"

sqldelight {
    databases {
        create("SabadPersistence") {
            packageName.set("farayan.sabad.db")
        }
    }
}

android {
    namespace 'farayan.sabad'
    lint {
        baseline = file("sabad-lint-baseline.xml")
        lintOptions {
            abortOnError false
        }
    }
    dexOptions {
        javaMaxHeapSize "5g"
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.8"
    }

    signingConfigs {
        FarayanKey {
            if (!project.hasProperty('SABAD_KEY_STORE_FILE_PATH') || !project.hasProperty('SABAD_KEY_STORE_FILE_PASS') || !project.hasProperty('SABAD_KEY_STORE_ALIAS_NAME') || !project.hasProperty('SABAD_KEY_STORE_ALIAS_PASS')) {
                throw new GradleException("Please define singing properties in ~/.gradle/gradle.properties like below:\n" +
                        "SABAD_KEY_STORE_FILE_PATH=/path/to/key/store/file\n" +
                        "SABAD_KEY_STORE_FILE_PASS=key-store-password\n" +
                        "SABAD_KEY_STORE_ALIAS_NAME=key-alias-name\n" +
                        "SABAD_KEY_STORE_ALIAS_PASS=key-alias-password\n")
            }
            storeFile file(SABAD_KEY_STORE_FILE_PATH)
            storePassword SABAD_KEY_STORE_FILE_PASS
            keyAlias SABAD_KEY_STORE_ALIAS_NAME
            keyPassword SABAD_KEY_STORE_ALIAS_PASS
        }
    }

    defaultConfig {
        multiDexEnabled true

        applicationId "farayan.sabad"
        minSdkVersion 24
        compileSdkVersion 34
        targetSdkVersion 34

        /**
         * 9
         * By default all group filters  skip deleted groups from being included in results
         */
        versionCode 9
        versionName "0.1.9"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        signingConfig signingConfigs.FarayanKey
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            applicationIdSuffix ".debug"
        }
        stage {
            initWith debug
            applicationIdSuffix ".stage"
            matchingFallbacks = ['debug']
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        maintenance {
            initWith debug
            applicationIdSuffix ""
            matchingFallbacks = ['debug']
            signingConfig signingConfigs.FarayanKey
        }

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                def formattedDate = new Date().format("yyyyMMdd.HHmmss")

                outputFileName = "" +
                        "${project.title}-" +
                        "${variant.versionCode}-" +
                        "${variant.versionCode}-" +
                        "${variant.buildType.name}-" +
                        "${variant.versionName}-" +
                        "${formattedDate}.apk"
            }
        }
    }

    flavorDimensions "publisher"
    productFlavors {
        farayan {
            dimension "publisher"
            versionNameSuffix = "-Farayan"
        }
        googlePlay {
            dimension "publisher"
            versionNameSuffix = "-GooglePlay"
        }
        cafebazaar {
            dimension "publisher"
            versionNameSuffix = "-Cafebazaar"
        }
    }
    buildFeatures {
        viewBinding true
        //dataBinding true
        compose true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

dependencies {
    implementation project(path: ':sabad-core')

    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation platform('com.google.firebase:firebase-bom:26.2.0')
    implementation "com.google.firebase:firebase-analytics"
    implementation 'com.google.firebase:firebase-messaging'

    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "com.google.android.material:material:1.2.1"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.beardedhen:androidbootstrap:2.3.1"
    implementation "io.github.inflationx:calligraphy3:3.1.1"
    implementation "io.github.inflationx:viewpump:2.0.3"
    implementation "com.android.support:multidex:1.0.3"
    implementation "com.journeyapps:zxing-android-embedded:4.1.0"
    implementation group: "commons-io", name: "commons-io", version: "2.8.0"

    implementation "androidx.activity:activity-compose:1.9.1"

    implementation "com.google.dagger:hilt-android:$hiltVersion"
    //implementation 'androidx.compose.material3:material3-android:1.2.1'
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"


    googlePlayImplementation "com.google.android.play:core-ktx:1.8.1"

    debugImplementation "com.facebook.stetho:stetho:1.5.1"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:1.5.0"
    debugImplementation "com.facebook.stetho:stetho-js-rhino:1.5.0"

    stageImplementation "com.facebook.stetho:stetho:1.5.1"
    stageImplementation "com.facebook.stetho:stetho-okhttp3:1.5.0"
    stageImplementation "com.facebook.stetho:stetho-js-rhino:1.5.0"

    maintenanceImplementation "com.facebook.stetho:stetho:1.5.1"
    maintenanceImplementation "com.facebook.stetho:stetho-okhttp3:1.5.0"
    maintenanceImplementation "com.facebook.stetho:stetho-js-rhino:1.5.0"
    maintenanceImplementation 'com.github.behzadian:debugghost:v1.1.3'

    testImplementation "junit:junit:4.13.1"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation "androidx.test.ext:junit:1.2.1"

    implementation "com.github.behzadian:android-font-awesome-brands-icon:$fontIconsVersion"
    //api project(path: ":font-awesome-brands-icon-lib")

    implementation "com.github.behzadian:android-font-awesome-solid-icon:$fontIconsVersion"
    //api project(path: ":font-awesome-solid-icon-lib")

    implementation "com.github.behzadian:android-font-awesome-regular-icon:$fontIconsVersion"
    //api project(path: ":font-awesome-regular-icon-lib")

    //implementation "farayan.farabank:farabank-core:$farabankCoreVersion"
    //implementation group: "farayan.android.apps", name: "farabank-core", version: "$farabankVersion", changing: true
    implementation project(path: ':farabank-core')

    //implementation "farayan.android.libs:farayan-commons:$farayanCommonsVersion"
    implementation project(path: ':farayan-commons')

    //implementation "com.github.behzadian:android-number-entry:$numberEntryVersion"
    //implementation project(path: ':number-entry')

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2024.08.00')
    implementation(composeBom)
    androidTestImplementation(composeBom)

    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.foundation:foundation-layout"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "com.google.accompanist:accompanist-themeadapter-material:0.28.0"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    // ViewModel utilities for Compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycleVersion"
    // Lifecycles only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    // Lifecycle utilities for Compose
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycleVersion"

    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycleVersion"

    implementation "com.google.accompanist:accompanist-permissions:0.35.1-alpha"

    implementation("io.coil-kt:coil:2.6.0")
    implementation("io.coil-kt:coil-compose:2.6.0")

    implementation "androidx.camera:camera-camera2:$camerax_stable_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_stable_version"
    implementation "androidx.camera:camera-view:$camerax_stable_version"

    //sqldelight
    implementation("app.cash.sqldelight:android-driver:$sqlDelightVer")
    implementation("app.cash.sqldelight:coroutines-extensions-jvm:$sqlDelightVer")
    implementation("app.cash.sqldelight:android-driver:$sqlDelightVer")
    implementation("app.cash.sqldelight:primitive-adapters:$sqlDelightVer")
}
afterEvaluate {
    publishing {
        publications {
            snapshot(MavenPublication) {
            }
        }
    }
}
// Allow references to generated code
kapt {
    correctErrorTypes true
}